plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.31'
    id 'io.qameta.allure' version '2.8.1'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.ricardorlg'
version = '1.0.4'

ext {
    isReleasedVersion = !project.version.endsWith("-SNAPSHOT")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation platform('software.amazon.awssdk:bom:2.15.82')
    api 'software.amazon.awssdk:devicefarm'
    implementation "io.arrow-kt:arrow-fx-coroutines:$arrow_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "io.arrow-kt:arrow-syntax:$arrow_version"
    implementation "io.github.microutils:kotlin-logging:$kotlin_logging_version"
    implementation "org.http4k:http4k-core:$http4k_version"
    implementation "com.jakewharton.picnic:picnic:0.5.0"

    testImplementation "org.slf4j:slf4j-simple:1.7.30"
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "io.kotest:kotest-runner-junit5:$kotest_version"
    testImplementation "io.kotest:kotest-assertions-core:$kotest_version"
    testImplementation "io.kotest:kotest-assertions-arrow-jvm:$kotest_version"
    testImplementation "io.kotest:kotest-extensions-allure:$kotest_version"
    testImplementation "io.kotest:kotest-property:$kotest_version"

}

allure {
    autoconfigure = false
    version = "$allure_version"
}

java {
    withSourcesJar()
    withJavadocJar()
}

jar {
    manifest {
        attributes("Implementation-Title": "Device Farm Tractor",
                "Implementation-Version": project.version.toString())
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlin.time.ExperimentalTime", "-Xuse-experimental=kotlin.io.path.ExperimentalPathApi"]
}

test {
    testLogging.showStandardStreams = true
    useJUnitPlatform()
    systemProperty("kotest.framework.parallelism", System.getProperty("kotest.framework.parallelism", "1"))
}

publishing {
    repositories {
        maven {
            name = "MavenCentral"
            credentials {
                username findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }

            if (!isReleasedVersion) {
                url "https://oss.sonatype.org/content/repositories/snapshots"
            } else {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            }
        }

        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ricardorlg/aws-device-farm-tractor")
            credentials {
                username = findProperty("GITHUB_ACTOR") ?: System.getenv("GITHUB_ACTOR")
                password = findProperty("GITHUB_TOKEN") ?: System.getenv("GITHUB_TOKEN")
            }
        }

    }
    publications {
        deviceFarmTractor(MavenPublication) {
            from(components.java)
            pom {
                name = "Device Farm Tractor"
                description = "Run Serenity BDD appium tests in AWS device farm"
                url = "https://github.com/ricardorlg/aws-device-farm-tractor"
                scm {
                    connection = "scm:git:git@github.com:ricardorlg/aws-device-farm-tractor.git"
                    developerConnection = "scm:git:ssh://github.com/ricardorlg/aws-device-farm-tractor.git"
                    url = "https://github.com/ricardorlg/aws-device-farm-tractor"
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id = 'ricardorlg'
                        name = 'Ricardo Larrahondo'
                        email = 'ricardolarrahondo2@hotmail.com'

                    }
                }

            }
        }
    }
}

signing {
    sign publishing.publications.deviceFarmTractor
}

compileKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

}


